plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'fabric-loom' version '1.12-SNAPSHOT'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    maven { url 'https://masa.dy.fi/maven' }
    maven { url 'https://api.modrinth.com/maven' }
    maven { url 'https://cursemaven.com' }
    maven { url "https://maven.terraformersmc.com" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "carpet:fabric-carpet:${project.carpet_core_version}"
    //modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    //modImplementation "maven.modrinth:carpet-extra:${project.carpet_extra_version}"
    //modImplementation "curse.maven:carpet-extra-349240:${project.carpet_extra_c_version}"
    //modImplementation "maven.modrinth:minitweaks:${project.mintweaks_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withJavadocJar()
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}"}
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = maven_group + "." + archives_base_name
            artifactId = archives_base_name + "-mc" + minecraft_version
            version = mod_version

            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
        maven {
            name "PixelCarpet"
            url = System.getenv("mavenURL")
            credentials {
                username = System.getenv("mavenUsername")
                password = System.getenv("mavenPassword")
            }
        }
    }
}
